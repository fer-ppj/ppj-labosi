<primarni_izraz> ::= IDN
	| BROJ
	| ZNAK
	| NIZ_ZNAKOVA
	| L_ZAGRADA <izraz> D_ZAGRADA
<postfiks_izraz> ::= <primarni_izraz>
	| <postfiks_izraz> L_UGL_ZAGRADA <izraz> D_UGL_ZAGRADA
	| <postfiks_izraz> L_ZAGRADA D_ZAGRADA
	| <postfiks_izraz> L_ZAGRADA <lista_argumenata> D_ZAGRADA
	| <postfiks_izraz> OP_INC
	| <postfiks_izraz> OP_DEC
<lista_argumenata> ::= <izraz_pridruzivanja>
	| <lista_argumenata> ZAREZ <izraz_pridruzivanja>
<unarni_izraz> ::= <postfiks_izraz>
	| OP_INC <unarni_izraz>
	| OP_DEC <unarni_izraz>
	| <unarni_operator> <cast_izraz>
<unarni_operator> ::= PLUS
	| MINUS
	| OP_TILDA
	| OP_NEG
<cast_izraz> ::= <unarni_izraz>
	| L_ZAGRADA <ime_tipa> D_ZAGRADA <cast_izraz>
<ime_tipa> ::= <specifikator_tipa>
	| KR_CONST <specifikator_tipa>
<specifikator_tipa> ::= KR_VOID
	| KR_CHAR
	| KR_INT
<multiplikativni_izraz> ::= <cast_izraz>
	| <multiplikativni_izraz> OP_PUTA <cast_izraz>
	| <multiplikativni_izraz> OP_DIJELI <cast_izraz>
	| <multiplikativni_izraz> OP_MOD <cast_izraz>
<aditivni_izraz> ::= <multiplikativni_izraz>
	| <aditivni_izraz> PLUS <multiplikativni_izraz>
	| <aditivni_izraz> MINUS <multiplikativni_izraz>
<odnosni_izraz> ::= <aditivni_izraz>
	| <odnosni_izraz> OP_LT <aditivni_izraz>
	| <odnosni_izraz> OP_GT <aditivni_izraz>
	| <odnosni_izraz> OP_LTE <aditivni_izraz>
	| <odnosni_izraz> OP_GTE <aditivni_izraz>
<jednakosni_izraz> ::= <odnosni_izraz>
	| <jednakosni_izraz> OP_EQ <odnosni_izraz>
	| <jednakosni_izraz> OP_NEQ <odnosni_izraz>
<bin_i_izraz> ::= <jednakosni_izraz>
	| <bin_i_izraz> OP_BIN_I <jednakosni_izraz>
<bin_xili_izraz> ::= <bin_i_izraz>
	| <bin_xili_izraz> OP_BIN_XILI <bin_i_izraz>
<bin_ili_izraz> ::= <bin_xili_izraz>
	| <bin_ili_izraz> OP_BIN_ILI <bin_xili_izraz>
<log_i_izraz> ::= <bin_ili_izraz>
	| <log_i_izraz> OP_I <bin_ili_izraz>
<log_ili_izraz> ::= <log_i_izraz>
	| <log_ili_izraz> OP_ILI <log_i_izraz>
<izraz_pridruzivanja> ::= <log_ili_izraz>
	| <postfiks_izraz> OP_PRIDRUZI <izraz_pridruzivanja>
<izraz> ::= <izraz_pridruzivanja>
	| <izraz> ZAREZ <izraz_pridruzivanja>
<slozena_naredba> ::= L_VIT_ZAGRADA <lista_naredbi> D_VIT_ZAGRADA
	| L_VIT_ZAGRADA <lista_deklaracija> <lista_naredbi> D_VIT_ZAGRADA
<lista_naredbi> ::= <naredba>
	| <lista_naredbi> <naredba>
<naredba> ::= <slozena_naredba>
	| <izraz_naredba>
	| <naredba_grananja>
	| <naredba_petlje>
	| <naredba_skoka>
<izraz_naredba> ::= TOCKAZAREZ
	| <izraz> TOCKAZAREZ
<naredba_grananja> ::= KR_IF L_ZAGRADA <izraz> D_ZAGRADA <naredba>
	| KR_IF L_ZAGRADA <izraz> D_ZAGRADA <naredba> KR_ELSE <naredba>
<naredba_petlje> ::= KR_WHILE L_ZAGRADA <izraz> D_ZAGRADA <naredba>
	| KR_FOR L_ZAGRADA <izraz_naredba> <izraz_naredba> D_ZAGRADA <naredba>
	| KR_FOR L_ZAGRADA <izraz_naredba> <izraz_naredba> <izraz> D_ZAGRADA <naredba>
<naredba_skoka> ::= KR_CONTINUE TOCKAZAREZ
	| KR_BREAK TOCKAZAREZ
	| KR_RETURN TOCKAZAREZ
	| KR_RETURN <izraz> TOCKAZAREZ
<prijevodna_jedinica> ::= <vanjska_deklaracija>
	| <prijevodna_jedinica> <vanjska_deklaracija>
<vanjska_deklaracija> ::= <definicija_funkcije>
	| <deklaracija>
<definicija_funkcije> ::= <ime_tipa> IDN L_ZAGRADA KR_VOID D_ZAGRADA <slozena_naredba>
	| <ime_tipa> IDN L_ZAGRADA <lista_parametara> D_ZAGRADA <slozena_naredba>
<lista_parametara> ::= <deklaracija_parametra>
	| <lista_parametara> ZAREZ <deklaracija_parametra>
<deklaracija_parametra> ::= <ime_tipa> IDN
	| <ime_tipa> IDN L_UGL_ZAGRADA D_UGL_ZAGRADA
<lista_deklaracija> ::= <deklaracija>
	| <lista_deklaracija> <deklaracija>
<deklaracija> ::= <ime_tipa> <lista_init_deklaratora> TOCKAZAREZ
<lista_init_deklaratora> ::= <init_deklarator>
	| <lista_init_deklaratora> ZAREZ <init_deklarator>
<init_deklarator> ::= <izravni_deklarator>
	| <izravni_deklarator> OP_PRIDRUZI <inicijalizator>
<izravni_deklarator> ::= IDN
	| IDN L_UGL_ZAGRADA BROJ D_UGL_ZAGRADA
	| IDN L_ZAGRADA KR_VOID D_ZAGRADA
	| IDN L_ZAGRADA <lista_parametara> D_ZAGRADA
<inicijalizator> ::= <izraz_pridruzivanja>
	| L_VIT_ZAGRADA <lista_izraza_pridruzivanja> D_VIT_ZAGRADA
<lista_izraza_pridruzivanja> ::= <izraz_pridruzivanja>
	| <lista_izraza_pridruzivanja> ZAREZ <izraz_pridruzivanja>
